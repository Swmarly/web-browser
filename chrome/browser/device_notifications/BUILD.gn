# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(!is_android)

source_set("device_notifications") {
  sources = []
  public_deps = []

  if (!is_android) {
    sources += [
      "device_connection_tracker.h",
      "device_system_tray_icon.h",
      "device_system_tray_icon_renderer.h",
    ]
    public_deps += [
      "//base",
      "//chrome/browser/profiles:profile",
      "//components/keyed_service/core",
      "//ui/gfx",
      "//url",
    ]

    if (!is_chromeos) {
      sources += [ "device_status_icon_renderer.h" ]
      public_deps += [
        "//chrome/browser/profiles:profile_util",
        "//chrome/browser/status_icons",
      ]
    } else {
      sources += [ "device_pinned_notification_renderer.h" ]
      public_deps += [
        "//ash/constants",
        "//ui/message_center/public/cpp",
      ]
    }
  }
}

source_set("impl") {
  sources = []
  deps = [ ":device_notifications" ]

  if (!is_android) {
    sources += [
      "device_connection_tracker.cc",
      "device_system_tray_icon.cc",
    ]

    deps += [
      "//chrome/browser/profiles:profile",
      "//chrome/browser/ui",
      "//content/public/browser",
      "//url",
    ]

    if (!is_chromeos) {
      sources += [ "device_status_icon_renderer.cc" ]
      deps += [
        "//base:i18n",
        "//chrome/app:branded_strings",
        "//chrome/app:command_ids",
        "//chrome/app:generated_resources",
        "//chrome/browser:browser_process",
        "//ui/base",
        "//ui/gfx",
        "//ui/gfx:color_utils",
      ]
    } else {
      sources += [ "device_pinned_notification_renderer.cc" ]
      deps += [ "//components/vector_icons" ]
    }
  }
  public_deps = [ "//chrome/browser:browser_public_dependencies" ]
}

source_set("unit_tests") {
  testonly = true
  sources = []
  deps = []
  public_deps = []

  if (!is_android) {
    sources += [
      "device_connection_tracker_unittest.cc",
      "device_connection_tracker_unittest.h",
      "device_system_tray_icon_unittest.cc",
      "device_system_tray_icon_unittest.h",
      "device_test_utils.cc",
      "device_test_utils.h",
    ]
    public_deps += [
      ":device_notifications",
      "//base",
      "//build:chromeos_buildflags",
      "//chrome/browser",
      "//chrome/browser/extensions",
      "//chrome/browser/extensions:test_support",
      "//chrome/test:test_support",
      "//extensions:test_support",
      "//extensions/common",
      "//testing/gmock",
      "//url",
    ]
  }

  if (!is_android && !is_chromeos) {
    sources += [
      "device_status_icon_unittest.cc",
      "device_status_icon_unittest.h",
    ]
    public_deps += [ "//chrome/browser/status_icons" ]
  }

  if (is_chromeos) {
    sources += [
      "device_pinned_notification_unittest.cc",
      "device_pinned_notification_unittest.h",
    ]
  }
}
