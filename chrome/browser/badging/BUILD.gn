# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(!is_android)

source_set("badging") {
  sources = [
    "badge_manager.h",
    "badge_manager_delegate.h",
    "badge_manager_factory.h",
  ]
  public_deps = [
    "//base",
    "//chrome/browser/profiles:profile",
    "//components/keyed_service/core",
    "//components/webapps/common",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//third_party/blink/public/common",
    "//url",
  ]

  if (is_win) {
    sources += [ "badge_manager_delegate_win.h" ]
  }

  if (is_mac) {
    sources += [ "badge_manager_delegate_mac.h" ]
  }
}

source_set("impl") {
  sources = [
    "badge_manager.cc",
    "badge_manager_factory.cc",
  ]
  deps = [
    ":badging",
    "//base:i18n",
    "//chrome/browser/ui/web_applications",
    "//chrome/browser/web_applications",
    "//chrome/browser/web_applications/proto",
    "//components/prefs",
    "//components/ukm",
    "//services/metrics/public/cpp:gen_ukm_builders",
    "//services/metrics/public/cpp:metrics_cpp",
    "//services/metrics/public/cpp:ukm_builders",
    "//ui/base",
    "//ui/strings:ui_strings",
    "//ui/strings:ui_strings_grit",
  ]

  if (is_win) {
    sources += [ "badge_manager_delegate_win.cc" ]
    deps += [ "//chrome/browser/ui/browser_window" ]
  }

  if (is_mac) {
    sources += [ "badge_manager_delegate_mac.cc" ]
    deps += [ "//chrome/browser/apps/app_shim" ]
  }

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]
}

source_set("test_support") {
  sources = []
  deps = []

  if (!is_android) {
    sources += [
      "test_badge_manager_delegate.cc",
      "test_badge_manager_delegate.h",
    ]
    public_deps = [
      ":badging",
      "//base",
      "//components/webapps/common",
    ]
    deps += [ "//chrome/browser/profiles:profile" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = []
  deps = []

  if (!is_android) {
    sources += [ "badge_manager_unittest.cc" ]
    deps += [
      ":badging",
      ":test_support",
      "//base/test:test_support",
      "//chrome/browser/web_applications",
      "//chrome/browser/web_applications:web_applications_test_support",
      "//chrome/test:test_support",
      "//components/ukm:test_support",
      "//content/test:test_support",
      "//extensions:test_support",
      "//extensions/common",
      "//services/metrics/public/cpp:gen_ukm_builders",
      "//services/metrics/public/cpp:ukm_builders",
      "//testing/gtest",
    ]
  }
}
