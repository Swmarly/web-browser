# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_win || is_mac || is_linux || is_chromeos)

import("//mojo/public/tools/bindings/mojom.gni")

# This is an experimental API and should not be used outside of its approved
# implementation. There is a visibility rule that ensures any new dependency on
# this target must be approved.
# TODO(crbug.com/433569400) Link to doc for more information.
mojom("mojom_experiment") {
  sources = [ "tab_strip_experiment_api.mojom" ]

  deps = [ ":mojom" ]

  webui_module_path = "/"
  visibility = [
    ":browser_tests",
    ":impl",
    ":impl_header",
    ":tab_strip_api",
    ":unit_tests",
  ]
}

mojom("mojom") {
  sources = [
    "tab_strip_api.mojom",
    "tab_strip_api_data_model.mojom",
    "tab_strip_api_events.mojom",
    "tab_strip_api_types.mojom",
  ]

  public_deps = [
    "//chrome/browser/ui/webui/tabs:mojo_bindings",
    "//components/tab_groups/public/mojom:mojo_bindings",
    "//mojo/public/mojom/base",
    "//url/mojom:url_mojom_gurl",
  ]

  webui_module_path = "/"

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "tabs_api.mojom.NodeId"
          cpp = "::tabs_api::NodeId"
        },
        {
          mojom = "tabs_api.mojom.NodeId.Type"
          cpp = "enum ::tabs_api::NodeId::Type"
        },
        {
          mojom = "tabs_api.mojom.Position"
          cpp = "::tabs_api::Position"
        },
        {
          mojom = "tabs_api.mojom.TabGroupVisualData"
          cpp = "::tab_groups::TabGroupVisualData"
        },
        {
          mojom = "tabs_api.mojom.Image"
          cpp = "::gfx::ImageSkia"
        },
        {
          mojom = "tabs_api.mojom.SplitTabVisualData"
          cpp = "::split_tabs::SplitTabVisualData"
        },
      ]
      traits_headers = [
        "//chrome/browser/ui/tabs/tab_strip_api/types/image_traits.h",
        "//chrome/browser/ui/tabs/tab_strip_api/types/node_id.h",
        "//chrome/browser/ui/tabs/tab_strip_api/types/node_id_traits.h",
        "//chrome/browser/ui/tabs/tab_strip_api/types/position.h",
        "//chrome/browser/ui/tabs/tab_strip_api/types/position_traits.h",
        "//chrome/browser/ui/tabs/tab_strip_api/types/tab_group_visual_data_traits.h",
        "//chrome/browser/ui/tabs/tab_strip_api/types/split_tab_visual_data_traits.h",
        "//components/tab_groups/tab_group_visual_data.h",
        "//ui/gfx/image/image_skia.h",
        "//components/tabs/public/split_tab_visual_data.h",
      ]
      traits_sources = [
        "//chrome/browser/ui/tabs/tab_strip_api/types/image_traits.cc",
        "//chrome/browser/ui/tabs/tab_strip_api/types/node_id_traits.cc",
        "//chrome/browser/ui/tabs/tab_strip_api/types/position_traits.cc",
        "//chrome/browser/ui/tabs/tab_strip_api/types/split_tab_visual_data_traits.cc",
        "//chrome/browser/ui/tabs/tab_strip_api/types/tab_group_visual_data_traits.cc",
      ]
      traits_public_deps = [
        "types",
        "//base",
        "//chrome/browser/ui/tabs",
      ]
    },
  ]

  ts_typemaps = [
    {
      source = "//chrome/browser/ui/tabs/tab_strip_api/node_id_converter.ts"
      types = [
        {
          converter = "NodeIdConverter"
          mojom = "tabs_api.mojom.NodeId"
          ts = "string"
        },
      ]
    },
  ]
}

source_set("tab_strip_api") {
  sources = [
    "tab_strip_service.h",
    "tab_strip_service_feature.h",
  ]

  public_deps = [
    ":mojom",
    "adapters",
    "//chrome/browser/ui/tabs:tab_strip_model_observer",
  ]

  deps = [ ":mojom_experiment" ]
}

# This target is needed to prevent circular dependency.
source_set("impl_header") {
  sources = [ "tab_strip_service_mojo_handler.h" ]

  deps = [
    ":mojom",
    ":mojom_experiment",
    ":tab_strip_api",
    "adapters",
    "observation",
    "//chrome/browser/ui/tabs/tab_strip_api/events",
  ]
}

source_set("impl") {
  sources = [
    "event_broadcaster.cc",
    "event_broadcaster.h",
    "tab_strip_service_impl.cc",
    "tab_strip_service_mojo_handler.cc",
    "tab_strip_service_mojo_handler.h",
  ]

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  deps = [
    ":impl_header",
    ":mojom_experiment",
    ":tab_strip_api",
    "adapters:impl",
    "converters",
    "events",
    "observation",
    "types",
    "utilities",
    "//chrome/browser/ui",
    "//chrome/browser/ui/browser_window",
    "//chrome/browser/ui/tabs:tab_strip",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "tab_strip_experiment_service_unittest.cc",
    "tab_strip_service_impl_unittest.cc",
  ]

  deps = [
    ":impl",
    ":mojom_experiment",
    ":tab_strip_api",
    "converters:unit_tests",
    "events:unit_tests",
    "observation:unit_tests",
    "testing",
    "types:unit_tests",
    "utilities:unit_tests",
    "//base/test:test_support",
    "//chrome/browser/ui/browser_window",
    "//chrome/browser/ui/tabs:tab_strip",
    "//chrome/browser/ui/tabs:test_support",
    "//chrome/test:test_support",
    "//components/tabs:public",
    "//content/test:test_support",
    "//testing/gtest",
  ]
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [ "tab_strip_service_impl_browsertest.cc" ]

  deps = [
    ":impl",
    ":mojom_experiment",
    ":tab_strip_api",
    "adapters/tree_builder:browser_tests",
    "observation",
    "//base",
    "//base/test:test_support",
    "//chrome/browser/ui:ui_features",
    "//chrome/browser/ui/browser_window",
    "//chrome/browser/ui/tabs:tab_strip",
    "//chrome/test:test_support",
    "//content/test:test_support",
    "//testing/gtest",
  ]
}
