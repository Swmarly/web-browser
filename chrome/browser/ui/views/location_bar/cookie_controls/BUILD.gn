# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

assert(is_win || is_mac || is_linux || is_chromeos)

source_set("cookie_controls") {
  sources = []

  if (toolkit_views) {
    sources += [
      "cookie_controls_bubble_coordinator.h",
      "cookie_controls_bubble_view.h",
      "cookie_controls_bubble_view_controller.h",
      "cookie_controls_bubble_view_impl.h",
      "cookie_controls_content_view.h",
      "cookie_controls_icon_view.h",
      "cookie_controls_page_action_controller.h",
    ]

    public_deps = [
      "//base",
      "//chrome/browser/ui/browser_window",
      "//chrome/browser/ui/user_education",
      "//chrome/browser/ui/views/frame:toolbar_button_provider",
      "//chrome/browser/ui/views/location_bar",
      "//chrome/browser/ui/views/page_action",
      "//components/content_settings/browser/ui",
      "//components/content_settings/core/common",
      "//components/favicon_base",
      "//components/tabs",
      "//content/public/browser",
      "//ui/base/metadata",
      "//ui/views",
    ]
  }
}

source_set("impl") {
  sources = []
  deps = [ ":cookie_controls" ]
  public_deps = [ "//chrome/browser:browser_public_dependencies" ]

  if (toolkit_views) {
    sources += [
      "cookie_controls_bubble_coordinator.cc",
      "cookie_controls_bubble_view.cc",
      "cookie_controls_bubble_view_controller.cc",
      "cookie_controls_bubble_view_impl.cc",
      "cookie_controls_content_view.cc",
      "cookie_controls_icon_view.cc",
      "cookie_controls_page_action_controller.cc",
    ]

    deps += [
      "//chrome/app:generated_resources",
      "//chrome/app/vector_icons",
      "//chrome/browser/content_settings:content_settings_factory",
      "//chrome/browser/favicon",
      "//chrome/browser/feedback",
      "//chrome/browser/profiles:profile",
      "//chrome/browser/ui:url_identity",
      "//chrome/browser/ui/browser_window",
      "//chrome/browser/ui/page_action:icon_type",
      "//chrome/browser/ui/user_education",
      "//components/feature_engagement/public",
      "//components/javascript_dialogs",
      "//components/omnibox/browser",
      "//components/omnibox/browser:vector_icons",
      "//components/privacy_sandbox:features",
      "//components/privacy_sandbox:tracking_protection_settings",
      "//components/user_education/common",
      "//components/vector_icons",
      "//ui/base",
      "//ui/gfx",
    ]
  }
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [
    "cookie_controls_bubble_browsertest.cc",
    "cookie_controls_content_view_browsertest.cc",
  ]

  deps = [
    ":cookie_controls",
    "//chrome/browser",  # Needed for browser.h and privacy_sandbox
    "//chrome/browser/ui/browser_window",
    "//chrome/test:test_support_ui",
    "//components/privacy_sandbox:features",
    "//components/privacy_sandbox:tracking_protection_prefs",
    "//components/strings",
    "//testing/gmock",
    "//testing/gtest",
  ]

  if (toolkit_views) {
    sources += [ "cookie_controls_bubble_view_browsertest.cc" ]
    deps += [
      "//net:test_support",
      "//ui/views:test_support",
    ]
  }
}

source_set("interactive_ui_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  sources = [ "cookie_controls_bubble_view_pixel_test.cc" ]

  deps = [
    ":cookie_controls",
    "//chrome/browser",  # Needed for browser.h and privacy_sandbox
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui",
    "//components/content_settings/core/test:test_support",
    "//components/privacy_sandbox:features",
    "//components/privacy_sandbox:tracking_protection_prefs",
    "//testing/gtest",
    "//ui/views:test_support",
    "//url",
  ]

  if (toolkit_views) {
    sources += [ "cookie_controls_interactive_uitest.cc" ]
    deps += [
      "//components/privacy_sandbox:tracking_protection_settings",
      "//components/user_education/views",
      "//net:test_support",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = []

  if (toolkit_views) {
    sources += [
      "cookie_controls_icon_view_unittest.cc",
      "cookie_controls_page_action_controller_unittest.cc",
    ]

    deps = [
      ":cookie_controls",
      "//chrome/browser/ui:ui_features",
      "//chrome/browser/ui/views/page_action:test_support",
      "//chrome/test:test_support",
      "//components/privacy_sandbox:features",
      "//components/tabs:test_support",
      "//testing/gmock",
      "//testing/gtest",
    ]
  }
}
