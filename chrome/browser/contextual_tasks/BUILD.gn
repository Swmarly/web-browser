# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

source_set("contextual_tasks") {
  sources = [
    "contextual_tasks_context_controller_factory.h",
    "contextual_tasks_service_factory.cc",
    "contextual_tasks_service_factory.h",
  ]

  if (!is_android) {
    sources += [
      "contextual_tasks_context_service_factory.cc",
      "contextual_tasks_context_service_factory.h",
    ]
  }

  deps = [
    "//base",
    "//chrome/browser/autocomplete:aim_eligibility_service",
    "//chrome/browser/favicon",
    "//chrome/browser/history",
    "//chrome/browser/signin",
    "//chrome/browser/sync",
    "//chrome/common:channel_info",
    "//components/contextual_tasks/internal",
    "//components/contextual_tasks/public",
    "//components/favicon/core",
    "//components/history/core/browser",
    "//components/sync/model",
    "//content/public/browser",
  ]
  if (!is_android) {
    deps += [ "//chrome/browser/passage_embeddings" ]
  }

  public_deps = [
    ":public",
    "//chrome/browser/profiles:profile",
  ]
}

source_set("public") {
  sources = [
    "contextual_tasks_context_controller.cc",
    "contextual_tasks_context_controller.h",
  ]

  if (!is_android) {
    sources += [
      "contextual_tasks_context_service.h",
      "tab_strip_context_decorator.h",
    ]
  }

  public_deps = [
    "//base",
    "//chrome/browser/profiles:profile",
    "//components/contextual_tasks/public",
    "//components/keyed_service/core",
    "//components/sessions:session_id",
  ]
  if (!is_android) {
    public_deps += [ "//chrome/browser/passage_embeddings" ]
  }
}

source_set("internal") {
  visibility = [
    ":*",
    "//chrome/browser",
  ]

  sources = [
    "contextual_tasks_context_controller_factory.cc",
    "contextual_tasks_context_controller_impl.cc",
    "contextual_tasks_context_controller_impl.h",
  ]
  if (!is_android) {
    sources += [ "contextual_tasks_context_service.cc" ]
  }

  deps = [
    ":contextual_tasks",
    ":public",
    "//components/contextual_tasks/public",
    "//components/keyed_service/core",
    "//components/omnibox/browser",
    "//components/sessions:session_id",
  ]
  if (!is_android) {
    deps += [
      "//chrome/browser/ui",
      "//chrome/browser/ui:browser_list",
    ]
  }
}

if (!is_android) {
  source_set("impl") {
    sources = [ "tab_strip_context_decorator.cc" ]

    deps = [
      ":public",
      "//base",
      "//chrome/browser/ui/tabs:tab_strip",
      "//components/contextual_tasks/public",
      "//content/public/browser",
    ]

    public_deps = [ "//chrome/browser:browser_public_dependencies" ]
  }

  source_set("ui") {
    sources = [
      "contextual_tasks_composebox_handler.cc",
      "contextual_tasks_composebox_handler.h",
      "contextual_tasks_navigation_throttle.cc",
      "contextual_tasks_navigation_throttle.h",
      "contextual_tasks_page_handler.cc",
      "contextual_tasks_page_handler.h",
      "contextual_tasks_side_panel_coordinator.cc",
      "contextual_tasks_side_panel_coordinator.h",
      "contextual_tasks_ui.cc",
      "contextual_tasks_ui.h",
      "contextual_tasks_ui_service.cc",
      "contextual_tasks_ui_service.h",
      "contextual_tasks_ui_service_factory.cc",
      "contextual_tasks_ui_service_factory.h",
    ]

    public_deps = [
      ":contextual_tasks",
      ":mojo_bindings",
      "//components/contextual_tasks/public:feature_list",
      "//content/public/browser",
      "//content/public/common",
      "//ui/webui",
      "//url",
    ]

    deps = [
      ":contextual_tasks",
      "//base",
      "//chrome/browser/profiles:profile",
      "//chrome/browser/resources/contextual_tasks:resources",
      "//chrome/browser/ui/browser_window",
      "//chrome/browser/ui/omnibox",
      "//chrome/browser/ui/views/side_panel",
      "//chrome/browser/ui/webui",
      "//chrome/browser/ui/webui/new_tab_page/composebox",
      "//chrome/browser/ui/webui/searchbox",
      "//chrome/browser/ui/webui/top_chrome",
      "//components/contextual_tasks/public:feature_list",
      "//components/keyed_service/core",
      "//components/omnibox/browser:mojo_bindings",
      "//ui/webui/resources/cr_components/composebox:mojo_bindings",
      "//url",
    ]
  }

  mojom("mojo_bindings") {
    sources = [ "contextual_tasks.mojom" ]
    webui_module_path = "/"
    public_deps = [
      "//mojo/public/mojom/base",
      "//url/mojom:url_mojom_gurl",
    ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "contextual_tasks_context_controller_factory_unittest.cc",
    "contextual_tasks_context_controller_impl_unittest.cc",
    "contextual_tasks_service_factory_unittest.cc",
  ]

  if (!is_android) {
    sources += [
      "contextual_tasks_context_service_factory_unittest.cc",
      "contextual_tasks_ui_service_unittest.cc",
      "tab_strip_context_decorator_unittest.cc",
    ]
  }

  deps = [
    ":contextual_tasks",
    ":internal",
    ":public",
    "//base",
    "//base/test:test_support",
    "//chrome/test:test_support",
    "//components/contextual_tasks/public",
    "//content/test:test_support",
    "//services/network/public/cpp",
    "//testing/gmock",
    "//testing/gtest",
  ]

  if (!is_android) {
    deps += [ ":ui" ]
  }
}
