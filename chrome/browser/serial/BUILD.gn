# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/guest_view/buildflags/buildflags.gni")

source_set("serial") {
  sources = [
    "chrome_serial_delegate.h",
    "serial_blocklist.h",
    "serial_chooser_context.h",
    "serial_chooser_context_factory.h",
    "serial_chooser_histograms.h",
    "serial_policy_allowed_ports.h",
    "web_serial_chooser.h",
  ]

  if (!is_android) {
    sources += [ "web_serial_chooser_desktop.h" ]
  } else {
    sources += [ "android/web_serial_chooser_android.h" ]
  }

  public_deps = [
    "//base",
    "//chrome/browser/profiles:profile",
    "//components/permissions",
    "//components/prefs",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//services/device/public/mojom",
    "//third_party/blink/public/common",
    "//url",
  ]
}

source_set("impl") {
  sources = [
    "chrome_serial_delegate.cc",
    "serial_blocklist.cc",
    "serial_chooser_context.cc",
    "serial_chooser_context_factory.cc",
    "serial_policy_allowed_ports.cc",
    "web_serial_chooser.cc",
  ]
  deps = [
    ":serial",
    "//base",
    "//chrome/app:generated_resources",
    "//chrome/browser:browser_process",
    "//chrome/browser/content_settings:content_settings_factory",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//chrome/browser/ui/serial",
    "//chrome/common:constants",
    "//components/content_settings/core/common",
    "//components/guest_view/buildflags",
    "//components/pref_registry",
    "//components/prefs",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//services/device/public/cpp/bluetooth",
    "//services/device/public/cpp/usb",
    "//services/device/public/mojom",
    "//ui/base",
    "//url",
  ]

  if (enable_guest_view) {
    deps += [ "//extensions/browser" ]
  }

  if (is_chromeos) {
    deps += [
      "//chrome/browser/ash/profiles",
      "//components/user_manager",
    ]
  }

  if (!is_android) {
    sources += [ "web_serial_chooser_desktop.cc" ]
  } else {
    sources += [
      "android/serial_bridge.cc",
      "android/web_serial_chooser_android.cc",
    ]
    deps += [ "//chrome/browser/serial/android:jni_headers" ]
  }

  public_deps = [ "//chrome/browser:browser_public_dependencies" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "serial_blocklist_unittest.cc",
    "serial_chooser_context_unittest.cc",
    "serial_policy_allowed_ports_unittest.cc",
  ]
  deps = [
    ":serial",
    "//base",
    "//base/test:test_support",
    "//chrome/browser/content_settings:content_settings_factory",
    "//chrome/browser/prefs",
    "//chrome/common:constants",
    "//chrome/test:test_support",
    "//components/account_id",
    "//components/content_settings/core/browser",
    "//components/content_settings/core/common",
    "//components/permissions:test_support",
    "//components/prefs:test_support",
    "//components/sync_preferences:test_support",
    "//content/test:test_support",
    "//google_apis",
    "//services/device/public/cpp:test_support",
    "//services/device/public/mojom",
    "//testing/gmock",
    "//testing/gtest",
    "//url",
  ]
}

if (is_linux || is_mac || is_win) {
  source_set("device_realtarget_browsertests") {
    testonly = true
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
    sources = [ "chrome_serial_realtarget_browsertest.cc" ]
    deps = [
      ":serial",
      "//base",
      "//base/test:test_support",
      "//chrome/browser/ui",
      "//chrome/browser/ui/serial",
      "//chrome/browser/ui/tabs:tab_strip",
      "//chrome/test:test_support_ui",
      "//content/public/browser",
      "//content/public/common",
      "//content/test:test_support",
      "//device/bluetooth",
      "//device/bluetooth/public/cpp",
    ]
  }
}

if (!is_android) {
  source_set("browser_tests") {
    testonly = true
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
    sources = [ "chrome_serial_browsertest.cc" ]
    deps = [
      ":serial",
      "//base",
      "//base/test:test_support",
      "//build:chromeos_buildflags",
      "//chrome/browser/ui",
      "//chrome/browser/ui/tabs:tab_strip",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//content/public/common",
      "//content/test:test_support",
      "//mojo/public/cpp/bindings",
      "//net:test_support",
      "//services/device/public/cpp:test_support",
      "//services/device/public/mojom",
      "//url",
    ]
  }
}
