# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/buildflag_header.gni")
import("//services/accessibility/buildflags.gni")

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags =
      [ "ENABLE_ACCESSIBILITY_SERVICE=$enable_accessibility_service_internal" ]
}

source_set("lib") {
  public_deps = [
    "//services/accessibility/public/mojom",
    "//ui/accessibility:ax_base",
  ]

  if (!is_chromeos && !is_fuchsia) {
    sources = [
      "automation_impl.cc",
      "automation_impl.h",
      "browser_accessibility_service.cc",
      "browser_accessibility_service.h",
    ]
  }
}

static_library("test_support") {
  testonly = true

  sources = [
    "fake_service_client.cc",
    "fake_service_client.h",
  ]

  deps = [
    ":buildflags",
    ":lib",
  ]
}

source_set("tests") {
  testonly = true

  deps = [
    ":lib",
    ":test_support",
    "//base/test:test_support",
    "//testing/gtest",
    "//ui/gfx:test_support",
  ]

  if (!is_chromeos && !is_fuchsia) {
    sources = [ "browser_accessibility_service_unittest.cc" ]
  }
}
