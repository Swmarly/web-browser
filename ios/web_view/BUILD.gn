# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/apple/tweak_info_plist.gni")
import("//build/config/ios/ios_sdk.gni")
import("//build/config/ios/rules.gni")
import("//build/config/locales.gni")
import("//components/grpc_support/include/headers.gni")
import("//ios/build/config.gni")
import("//ios/web/public/js_messaging/optimize_js.gni")
import("//ios/web_view/features.gni")
import("//ios/web_view/framework/sources.gni")

_package_dir = "$root_out_dir/ios_web_view"

action("ios_web_view_generate_license") {
  _license_path = "$_package_dir/LICENSE"

  script = "//tools/licenses/licenses.py"
  inputs = [ "//buildtools/$host_os/gn" ]
  outputs = [ _license_path ]
  args = [
    "license_file",
    rebase_path(_license_path, root_build_dir),
    "--gn-target",
    "//ios/web_view/framework:web_view",
    "--gn-out-dir",
    ".",
    "--target-os",
    "ios",
  ]
}

copy("ios_web_view_package_copy") {
  sources = [
    "$root_out_dir/$ios_web_view_output_name.framework",
    "//AUTHORS",
    "//chrome/VERSION",
  ]
  outputs = [ "$_package_dir/{{source_file_part}}" ]

  deps = [ "//ios/web_view/framework:web_view" ]
}

if (enable_dsyms) {
  action("ios_web_view_dsym_archive") {
    script = "//chrome/tools/build/mac/archive_symbols.py"

    # These are the dSYMs that will be archived. The sources list must be
    # the target outputs that correspond to the dSYMs (since a dSYM is a
    # directory it cannot be listed as a source file). The targets that
    # generate both the dSYM and binary image are listed in deps.
    _dsyms = [ "$root_out_dir/$ios_web_view_output_name.dSYM" ]

    sources = [ "$root_out_dir/$ios_web_view_output_name.framework" ]

    _output = "$_package_dir/$ios_web_view_output_name.dSYM.tar.bz2"

    outputs = [ _output ]

    args = [ rebase_path(_output, root_build_dir) ] +
           rebase_path(_dsyms, root_build_dir)

    deps = [ "//ios/web_view/framework:web_view" ]
  }
} else {
  group("ios_web_view_dsym_archive") {
  }
}

group("ios_web_view_package") {
  deps = [
    ":ios_web_view_dsym_archive",
    ":ios_web_view_generate_license",
    ":ios_web_view_package_copy",
  ]
}
